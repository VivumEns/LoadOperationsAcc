// @strict-types


#Область ОписаниеПеременных

// Код описания переменных

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбъектЗначение = РеквизитФормыВЗначение("Объект");
	МетаданныеОбработки = ОбъектЗначение.Метаданные();
	Заголовок = МетаданныеОбработки.Синоним + " " + МетаданныеОбработки.Комментарий + ". Файл: ";

	СистемнаяИнформация = Новый СистемнаяИнформация;
	Объект.ВерсияПриложения = СистемнаяИнформация.ВерсияПриложения;
	
	Если Лев(Объект.ВерсияПриложения, 3) = "8.2" Тогда
		Объект.МодальностьРазрешена = Истина;
	Иначе
		//@skip-check server-execution-safe-mode
		Выполнить("Объект.МодальностьРазрешена = Метаданные.РежимИспользованияМодальности = Метаданные.СвойстваОбъектов.РежимИспользованияМодальности.Использовать;");
	КонецЕсли;
	
	Объект.РежимСовместимости = Строка(Метаданные.РежимСовместимости); // Метаданные.СвойстваОбъектов.РежимСовместимости.Версия8_3_5

	СтруктураХранилища = Новый Структура;
	//@skip-check form-self-reference
	СтруктураХранилища.Вставить("УникальныйИдентификаторФормы", ЭтаФорма.УникальныйИдентификатор);
	//@skip-check form-self-reference
	Объект.ПутьКХранилищу = ПоместитьВоВременноеХранилище(СтруктураХранилища, ЭтаФорма.УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Команда_ЗагрузитьТЗ(Команда)

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ОбъектПутьКХранилищу", Объект.ПутьКХранилищу);

	ОткрытьФормуАвтоматически("ФормаТаблицаЗначений", ПараметрыОткрытия, "", Неопределено);

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуАвтоматически(ИмяФормы, ПараметрыОткрытияФормы = Неопределено, ИмяПроцедуры = "", ПараметрыПроцедуры = Неопределено)

    ПутьКФорме = ПолучитьИмяОбработки() + ".Форма." + ИмяФормы;

	Если Объект.МодальностьРазрешена Тогда
	
		КодВыполнения = "
		|ОписаниеВыбранныхТипов = ОткрытьФормуМодально(ПутьКФорме, ПараметрыОткрытияФормы);
		|" + ИмяПроцедуры + ?(ПустаяСтрока(ИмяПроцедуры), "", "(ОписаниеВыбранныхТипов, ПараметрыПроцедуры);");
	
	Иначе
	
		КодВыполнения = "
		|" + ?(ПустаяСтрока(ИмяПроцедуры), "", "Оповещение = Новый ОписаниеОповещения(" + """" + ИмяПроцедуры + """" + ", ЭтаФорма, ПараметрыПроцедуры);") + "
		|ОткрытьФорму(ПутьКФорме, ПараметрыОткрытияФормы, , , , , " + ?(ПустаяСтрока(ИмяПроцедуры), "", "Оповещение") + ", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);"; 
		
	КонецЕсли;
	
	Выполнить(КодВыполнения);

КонецПроцедуры

&НаСервере
Функция ПолучитьИмяОбработки()
	
	Возврат РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	
КонецФункции 

#КонецОбласти
